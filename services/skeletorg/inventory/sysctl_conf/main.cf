bundle agent inventory_sysctl_conf_main
# @brief Inventory configuration settings found in sysctl.conf
# Note: This is for the configured settings, not the currently active settings.
{
  vars:
      "sysctl_conf" string => "/etc/sysctl.conf";
      "sysctl_conf_data" data => data_readstringarray($(sysctl_conf),
                                                      "\s*#[^\n]*",
                                                      "\s?+=\s?+",
                                                      100,
                                                      1M);
      "sysctl_keys" slist => getindices("sysctl_conf_data");
      "sysctl[$(sysctl_keys)]"
        string => "$(sysctl_conf_data[$(sysctl_keys)])",
        meta => { "inventory", "attribute_name=Sysctl Config $(sysctl_keys)" },
        comment =>
          "Inventory each configured sysctl value so that we can report on it and view differences across the environment before we make policy changes.";

  reports:
    DEBUG|DEBUG_inventory_sysctl_conf_main::
      "$(this.bundle) activated";
      "$(sysctl_keys) = $(sysctl_conf_data[$(sysctl_keys)])";
}
